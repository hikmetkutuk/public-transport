import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

subprojects {
	if (project.name == 'app') {
		tasks.withType(BootJar) {
			mainClass.set('com.app.Application')
		}
	}
}

spotless {
	java {
		target fileTree('.') {
			include '**/*.java'
			exclude '**/build/**', '**/build-*/**'
		}
		toggleOffOn()
		palantirJavaFormat()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

tasks.register('copyAppJar') {
	dependsOn ':app:bootJar'
	doLast {
		copy {
			from(file('app/build/'))
			into(file('./build/'))
		}
		println 'app JAR dosyası kök proje dizinine kopyalandı.'
	}
}

tasks.register('buildCycle') {
	group = 'build'
	dependsOn 'spotlessApply', ':app:build', ':app:bootJar', 'copyAppJar'
	doLast {
		println 'Format, build ve bootJar görevleri başarıyla tamamlandı, JAR dosyası kök projeye kopyalandı.'
	}
}

tasks.named('build') {
	dependsOn tasks.named('buildCycle')
}

tasks.named('bootJar') {
	archiveBaseName.set('app')
	archiveVersion.set('0.0.1-SNAPSHOT')
}

tasks.named('jar') {
	enabled = false
}

tasks.register('runJar', Exec) {
	group = 'application'
	description = 'Run the generated JAR file'
	commandLine 'java', '-jar', 'build/libs/app-0.0.1-SNAPSHOT.jar'
}

tasks.register('fullCycle') {
	group = 'application'
	dependsOn 'buildCycle', 'runJar'
	doLast {
		println 'Format, build ve bootJar görevleri başarıyla tamamlandı, JAR dosyası kök projeye kopyalandı ve çalıştırıldı.'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
